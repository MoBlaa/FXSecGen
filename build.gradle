buildscript {
    ext {
        // Dependencies
        lombok_version = "1.16.20"
        zxing_version = "3.3.2"
        jetbrains_annotations_version = "16.0.1"
        jbcrypt_version = "0.4"
        bc_version = "1.59"
        gson_version = "2.8.2"

        // Plugins
        gradle_extra_plugin_version = "3.0.3"
    }
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:$gradle_extra_plugin_version"
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'nebula.provided-base'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "org.myshelf.fxencoder.Main"

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
            includes = ["**/*.fxml"]
        }
    }
}

dependencies {
    // Encryptions
    compile "org.mindrot:jbcrypt:$jbcrypt_version"
    compile "org.bouncycastle:bcprov-jdk15on:$bc_version"

    // Serialization
    compile "com.google.code.gson:gson:$gson_version"

    // For Java Null-safety
    compile "org.jetbrains:annotations:$jetbrains_annotations_version"

    // For java boilerplate enhancement
    provided "org.projectlombok:lombok:$lombok_version"

    // For QR-Code generation
    compile "com.google.zxing:core:$zxing_version"
    compile "com.google.zxing:javase:$zxing_version"
}

jar {
    manifest {
        attributes 'Main-Class': "org.myshelf.fxencoder.Main",
                'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
    }
    from (configurations.compile.collect { it.isDirectory() ? it : zipTree(it)}) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }
}